// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:

    //zx zy
    Mux16(a=x, sel=zx, out=zxOut);
    Mux16(a=y, sel=zy, out=zyOut);
  
    //nx ny
    Not16(in=zxOut, out=notZxOut);
    Not16(in=zyOut, out=notZyOut);
    Mux16(a=zxOut, b=notZxOut, sel=nx, out=nzxo);
    Mux16(a=zyOut, b=notZyOut, sel=ny, out=nzyo);

    //f
    Add16(a=nzxo, b=nzyo, out=Add);
    And16(a=nzxo, b=nzyo, out=And);
    Mux16(a=And, b=Add, sel=f, out=fRst);

    //no
    Not16(in=fRst, out=notfRst);
    Mux16(a=fRst, b=notfRst, sel=no,out=out, out[0..7]=rstL, out[8..15]=rstR, out[15]=rstSt);

    //zr
    Or8Way(in=rstL, out=rstLrst);
    Or8Way(in=rstR, out=rstRrst);
    Or(a=rstLrst, b=rstRrst, out=zrrst);
    Not(in=zrrst, out=zr);

    //ng
    And(a=rstSt, b=true, out=ng);


    //// Replace this comment with your code.
}